;; The first three lines of this file were inserted by DrRacket. They record metadata
;; about the language level of this file in a form that our tools can easily process.
#reader(lib "htdp-intermediate-lambda-reader.ss" "lang")((modname rectangle) (read-case-sensitive #t) (teachpacks ()) (htdp-settings #(#t constructor repeating-decimal #f #t none #f () #t)))
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;; Zijie Jiang  (20714726)
;; CS135 Fall 2017
;; Assignment 09, Question 02
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

(require "rectanglelib.rkt")

(define-struct cell (num used?))
;; A Cell is a (make-cell Nat Bool)

;; A Grid is a (listof (listof Cell))
;; requires: the grid contains a non-empty list of non-empty lists,
;;  all the same length.

(define-struct rect (x y w h))
;; A Rect is a (make-rect Nat Nat Nat Nat)

(define-struct state (grid rects))
;; A State is a (make-state Grid (listof Rect))


;; Here are a couple of constants that can be used to define
;; the puzzle in the assignment, and a random larger puzzle.

(define puzz '((0 0 0 0 0 5 0)
               (0 0 0 0 0 2 2)
               (0 3 0 6 3 2 0)
               (4 0 0 0 0 0 0)
               (0 0 0 4 0 4 0)
               (2 0 6 0 2 4 0)
               (0 0 0 0 0 0 0)))

(define big-puzz '((4 0 7 0 0 0 0 0 0 0 0 21 0)
                   (0 3 2 0 0 0 0 0 0 0 0 0 2)
                   (0 0 0 0 0 0 0 2 3 0 0 0 0)
                   (0 0 0 20 0 0 0 0 0 0 0 0 5)
                   (0 2 0 0 0 0 0 4 0 0 0 0 0)
                   (0 0 3 0 0 0 0 0 0 0 0 0 0)
                   (3 0 0 0 0 5 2 4 0 0 0 0 0)
                   (0 0 0 0 0 2 0 6 0 0 0 0 0)
                   (0 0 0 20 0 0 0 0 0 0 0 0 0)
                   (0 0 0 0 0 0 0 0 0 0 0 0 0)
                   (0 0 0 0 0 0 0 0 0 0 0 24 0)
                   (0 0 0 0 4 0 4 0 0 0 4 0 0)
                   (0 0 3 0 0 0 0 0 0 0 8 0 2)))

(define first-unused-at-2-1
  (make-state
   (list (list
          (make-cell 0 true)
          (make-cell 1 true)
          (make-cell 2 true))
         (list
          (make-cell 3 true)
          (make-cell 4 true)
          (make-cell 5 false))
         (list
          (make-cell 6 false)
          (make-cell 7 false)
          (make-cell 8 false))) empty))

(define state1
  (make-state
   (list (list
          (make-cell 0 true)
          (make-cell 0 true)
          (make-cell 2 true))
         (list
          (make-cell 0 true)
          (make-cell 4 true)
          (make-cell 5 false))
         (list
          (make-cell 6 false)
          (make-cell 7 false)
          (make-cell 8 false))) empty))

(define sample-state
  (make-state
   (list (list
          (make-cell 0 true)
          (make-cell 0 true)
          (make-cell 0 true)
          (make-cell 2 true)
          (make-cell 0 true)
          (make-cell 2 true)
          (make-cell 0 true))
         (list
          (make-cell 0 true)
          (make-cell 0 true)
          (make-cell 0 false)
          (make-cell 0 false)
          (make-cell 0 false)
          (make-cell 4 false)
          (make-cell 0 true))
         (list
          (make-cell 0 true)
          (make-cell 3 true)
          (make-cell 6 false)
          (make-cell 0 false)
          (make-cell 0 false)
          (make-cell 0 false)
          (make-cell 0 true))
         (list
          (make-cell 4 true)
          (make-cell 2 false)
          (make-cell 0 false)
          (make-cell 0 false)
          (make-cell 0 false)
          (make-cell 4 false)
          (make-cell 0 true))
         (list
          (make-cell 0 false)
          (make-cell 0 false)
          (make-cell 0 false)
          (make-cell 2 false)
          (make-cell 0 false)
          (make-cell 0 false)
          (make-cell 0 true))
         (list
          (make-cell 0 false)
          (make-cell 0 false)
          (make-cell 0 false)
          (make-cell 0 false)
          (make-cell 10 false)
          (make-cell 0 false)
          (make-cell 7 true))
         (list
          (make-cell 3 false)
          (make-cell 0 false)
          (make-cell 0 false)
          (make-cell 0 false)
          (make-cell 0 false)
          (make-cell 0 false)
          (make-cell 0 true)))
   empty))
(define 2x2-puzz1 '((2 0)
                   (0 2)))
(define 2x2-puzz1-soln 
'((make-rect)))

          


;; ====================== (A) ==========================
;; (map2d f nested-lst) consumes a function f and a list of lists
;;   of values nested-lst, and produces a new list of lists in which
;;   f has been applied to every element of the input.
;; map2d: (X > Y) (listof (listof X)) -> (listof (listof Y))

(define (map2d f nested-lst)
  (map (lambda (lst) (map f lst)) nested-lst))


;; ====================== (b) ===========================
;; (construct-puzzle nested-lst) consumes nested-lst and produces
;;   a State representing the initial state of a puzzle
;; construct-puzzle: (listof (listof Nat)) -> State
;; Examples

(define (construct-puzzle nested-lst)
  (make-state (map2d (lambda (elem) (make-cell elem false)) nested-lst)
              empty))


;; ====================== (c) ===========================
;; (solved? state) consumes state and produces a boolean value
;;   that indicates whether the puzzle described by state is
;;   fully solved
;; solved?: State -> Bool
;; Example

(define (solved? state)
  (foldr (lambda (flst rlst)
           (local [(define row-result
                     (foldr (lambda (f r)
                              (cond [(false? (cell-used? f)) false]
                                    [else r]))
                            true flst))]
             (cond [(false? row-result) false]
                   [else rlst])))
           true (state-grid state)))


;; ====================== (d) ===========================
;; (get-first-unused grid) consumes grid and finds the topmost,
;;   leftmost cell in the grid that isn't marked as used.
;; get-first-unused: Grid -> (list Nat Nat)
;; requires: grid has at least one unused cell

(define (get-first-unused grid)
  (local [;; (find-unused-row/acc row x) produces the x-coordinate,
          ;;   x, of the first unused-cell in the row.
          ;; find-unused-row/acc: (listof Cell) Nat -> (anyof Nat false)
          
          (define (find-unused-row/acc row x)
            (cond [(empty? row) false]
                  [(not (cell-used? (first row))) x]
                  [else
                   (find-unused-row/acc (rest row) (add1 x))]))

          ;; (find-unused-grid/acc grid y) produces the (x,y) pair
          ;;   of the first unused cell of grid, using y as
          ;;   accumulator.
          ;; find-unused-grid/acc: Grid Nat
          ;;                       -> (anyof false (list Nat Nat))
          
          (define (find-unused-grid/acc grid y)
            (local [(define row-result (find-unused-row/acc (first grid) 0))]
              (cond [(false? row-result)
                     (find-unused-grid/acc (rest grid) (add1 y))]
                    [else
                     (list row-result y)])))]
    (find-unused-grid/acc grid 0)))


;; ====================== (e) ===========================
;; (rect-in-grid rect grid) returns the grid (nested list of cells)
;;   defined by rect, in the grid parameter
;; rect-in-grid: Rect Grid -> Grid

(define (rect-in-grid rect grid)
            (local [(define rect_x (rect-x rect))
                    (define rect_y (rect-y rect))
                    (define rect_w (rect-w rect))
                    (define rect_h (rect-h rect))
                    ;; (rect-in-grid-row/acc row x) produces the list
                    ;;   of cells in row, defined by rect_x and
                    ;;   rect_x + rect_w, using x as accumulator
                    ;; rect-in-grid-row/acc: (listof Cell) Nat
                    ;;                       -> (listof Cell)
                    
                    (define (rect-in-grid-row/acc row x)
                      (cond [(= x (+ rect_x rect_w))
                             empty]
                            [(empty? row) empty]
                            [(and (>= x rect_x)
                                  (< x (+ rect_x rect_w)))
                             (cons (first row)
                                   (rect-in-grid-row/acc (rest row) (add1 x)))]
                            [else
                             (rect-in-grid-row/acc (rest row) (add1 x))]))

                    ;; (rect-in-grid/acc grid y) produces the grid of
                    ;;    cells in grid, defined by rect, using y as
                    ;;    accumulator
                    ;; rect-in-grid/acc: Grid Nat -> Grid
                    
                    (define (rect-in-grid/acc grid y)
                      (cond [(= y (+ rect_y rect_h)) empty]
                            [(empty? grid) empty]
                            [(and (>= y rect_y)
                                  (< y (+ rect_y rect_h)))
                             (cons (rect-in-grid-row/acc (first grid) 0)
                                   (rect-in-grid/acc (rest grid) (add1 y)))]
                            [else
                             (rect-in-grid/acc (rest grid) (add1 y))]))]
              (rect-in-grid/acc grid 0)))


;; (contains-used-cell? rect grid) produces true if the Rectangle
;;   in grid, defined by rect, contains one or more used cell, and
;;   false otherwise.
;; contains-used-cell?: Rect Grid -> Bool

(define (contains-used-cell? rect grid)
  (local [(define rectangle (rect-in-grid rect grid))
          ;; (contains-used-cell/row? row) produces true
          ;;   if row contains one or more used cells, and
          ;;   false otherwise
          ;; contains-used-cell/row?: (listof Cell) -> Bool
          
          (define (contains-used-cell/row? row)
            (foldr (lambda (f r) (cond [(cell-used? f) true]
                                       [else r])) false row))]
  (foldr (lambda (flst rlst)
           (cond [(contains-used-cell/row? flst) true]
                 [else rlst]))
         false rectangle)))


;; (contains-one-number? rect grid) produces true if the Rectangle
;;   in grid, defined by rect, contains exactly one non-empty cell,
;;   and false otherwise
;; contains-one-number?: Rect Grid -> Bool

(define (contains-one-number? rect grid)
  (local [(define rectangle (rect-in-grid rect grid))
          ;; (count-non-zero-number-row row) produces the number
          ;;   of non-empty cells in row.
          ;; count-non-zero-number-row: (listof Cell) -> Nat
          
          (define (count-non-zero-number-row row)
            (length (filter (lambda (x) (not (zero? (cell-num x)))) row)))

          ;; (count-non-zero-number-rect rect) produces the number
          ;;   of non-empty cells in rect (the sub-grid in grid)
          ;; count-non-zero-number-rect: Grid -> Nat
          
          (define (count-non-zero-number-rect rect)
            (foldr (lambda (flst rlst)
                     (+ (count-non-zero-number-row flst) rlst)) 0 rect))]
    (= 1 (count-non-zero-number-rect rectangle))))


;; (num-equal-area? rect grid) produces true if the Rectangle
;;   in grid, defined by rect, has an area equal to the number
;;   in rect's only non-empty cell, and false if they are not
;;   equal.
;; num-equal-area?: Rect Grid -> Bool

(define (num-equal-area? rect grid)
  (local [(define rectangle (rect-in-grid rect grid))
          (define only-num
            (cell-num
             (first (first
              (filter cons?
                      (foldr (lambda (flst rlst)
                               (cons (filter (lambda (x)
                                               (not (zero? (cell-num x))))
                                             flst) rlst))
                             empty rectangle))))))
          (define area (* (rect-w rect) (rect-h rect)))]
    (= only-num area)))
                     

;; (valid-rect? rect grid) produces true if the Rectangle
;;   in grid, defined by rect, is considered valid under
;;   the rule of the rectangle puzzle, and false if it is
;;   invalid.
;; valid-rect?: Rect Grid -> Bool

(define (valid-rect? rect grid)
  (local [(define rectangle (rect-in-grid rect grid))]
    (and (not (contains-used-cell? rect grid))
         (contains-one-number? rect grid)
         (num-equal-area? rect grid))))


;; (shade-used-cell rect grid) produces a Grid where all cells 
;;    covered by rect in grid are marked as used.
;; shade-used-cell: Rect Grid -> Grid

(define (shade-used-cell rect grid)
  (local [;; (shade-row/index row index) marks the cells covered
          ;;   by rect in row to be used, using index as accumulator
          ;; shade-row/index: (listof Cell) Nat -> (listof Cell)
          
          (define (shade-row/index row index)
            (cond [(empty? row) empty]
                  [(< index (rect-x rect))
                   (cons (first row) (shade-row/index (rest row) (add1 index)))]
                  [(>= index (+ (rect-x rect) (rect-w rect))) row]
                  [else
                   (cons (make-cell (cell-num (first row)) true)
                         (shade-row/index (rest row) (add1 index)))]))

          ;; (shade-grid/index grid index) marks the cells covered
          ;;   by rect in grid to be used, using index as accumulator
          ;; shade-grid/index: Grid Nat -> Grid
          
          (define (shade-grid/index grid index)
            (cond [(empty? grid) empty]
                  [(< index (rect-y rect))
                   (cons (first grid) (shade-grid/index (rest grid) (add1 index)))]
                  [(>= index (+ (rect-y rect) (rect-h rect))) grid]
                  [else
                   (cons (shade-row/index (first grid) 0)
                         (shade-grid/index (rest grid) (add1 index)))]))]
    (shade-grid/index grid 0)))


;; (neighbours state) consumes state and produces a list of
;;   new states that might legitimately follow from the given
;;   state after adding a single rectangle.
;; neighbours: State -> (listof State)
;; Example

(define (neighbours state)
  (local [(define grid (state-grid state))
          (define first-unused-cell
            (get-first-unused grid))
          (define first-unused-x
            (first first-unused-cell))
          (define first-unused-y
            (second first-unused-cell))
          ;; (search-right/index grid cur-y cur-x) searches
          ;;   for all possible next state from the parameter
          ;;   state, in a row defined by grid[cur-y], using
          ;;   cur-x as accumulator.
          ;; search-right/index: Grid Nat Nat -> (listof State)
          
          (define (search-right/index grid cur-y cur-x)
            (cond [(>= cur-x (length (first grid))) empty]
                  [(< cur-x first-unused-x)
                   (search-right/index grid (add1 cur-x))]
                  [else
                   (local [(define rect
                             (make-rect first-unused-x
                                        first-unused-y
                                        (add1 (- cur-x first-unused-x))
                                        (add1 (- cur-y first-unused-y))))]
                     (cond [(valid-rect? rect grid)
                            (cons (make-state (shade-used-cell rect grid)
                                              (cons rect (state-rects state)))
                                  (search-right/index
                                   grid cur-y (add1 cur-x)))]
                           [else
                            (search-right/index grid cur-y (add1 cur-x))]))]))

          ;; (search-down/index grid cur-y) searches for all
          ;;   possible next state from the parameter state
          ;;   in grid, using cur-y as accumulator
          ;; search-down/index: Grid Nat -> (listof State)
          
          (define (search-down/index grid cur-y)
            (cond [(>= cur-y (length grid)) empty]
                  [(< cur-y first-unused-y)
                   (search-down/index grid (add1 cur-y))]
                  [else
                   (append (search-right/index grid cur-y first-unused-x)
                           (search-down/index grid (add1 cur-y)))]))]
    (search-down/index (state-grid state) first-unused-y)))
    

;; ========================= (f) ===============================
;; (solve-rectangle-puzzle puzzle) consumes puzzle and attempts
;;   to solve the puzzle, producing either the list of rectangles
;;   that describe a solution if one exists, or false if no solution
;;   can be found
;; solve-rectangle-puzzle: (listof (listof Nat))
;;                         -> (anyof (listof Rect) false)

(define (solve-rectangle-puzzle puzzle)
  (local [(define result (search solved? neighbours
                                 (construct-puzzle puzzle)))]
    (cond [(false? result) false]
          [else
           (state-rects result)])))
